---
description: |
  The codiscrete topology on a type.
---
<!--
```agda
open import Cat.Displayed.Total
open import Cat.Functor.Adjoint
open import Cat.Prelude

open import Data.Power
open import Data.Sum

open import Topology.Instances.Cofree
open import Topology.Base

```
-->
```agda
module Topology.Instances.Codiscrete where
```

# The codiscrete topology {defines="codiscrete-topology"}

For motivation, recall that the [[discrete topology]] is the finest
topology one can impose on a set $X$. The **codiscrete topology** is dual
to the discrete topology: it is the *coarsest* topology possible on $X$.

Classically, this topology can be constructed by taking only the empty
set and the entirety of $X$ as open sets. Unfortunately, this does not
work constructively, as cannot show that this set is closed under infinitary
unions!

Instead, we opt to define the codiscrete topology on $X$ as the [[cofree topology]]
generated by the empty set.

<!--
```agda
private variable
  ℓ ℓ' : Level
  X Y : Type ℓ

open Cofree-object
open is-continuous
open Topology-on
open Total-hom
```
-->

```agda
Codiscrete-topology : (X : Type ℓ) → Topology-on X
Codiscrete-topology X = Cofree-topology λ _ → ⊥Ω
```

Though less conceptually clear, this definition gives us the right
universal property: every function $f : X \to Y$ is continuous with
respect to the codiscrete topology on $Y$, regardless of the topology
on $X$!

```agda
codiscrete-continuous
  : ∀ {T : Topology-on X}
  → {f : X → Y}
  → is-continuous f T (Codiscrete-topology Y)
codiscrete-continuous {T = T} {f = f} =
  cofree-continuous $
  elim! (λ U ff → absurd ff)
```

Put another way, codiscrete topologies are cofree objects relative to
the forgetful functor $U : \Top \to \Sets$.

```agda
Codiscrete-topology-cofree : ∀ (X : Set ℓ) → Cofree-object Topologies↪Sets X
Codiscrete-topology-cofree X .cofree = X , Codiscrete-topology ⌞ X ⌟
Codiscrete-topology-cofree X .counit x = x
Codiscrete-topology-cofree X .unfold f .hom = f
Codiscrete-topology-cofree X .unfold f .preserves = codiscrete-continuous
Codiscrete-topology-cofree X .commute = refl
Codiscrete-topology-cofree X .unique g p = ext (apply p)
```

We can assemble these cofree objects together to form a right adjoint
to the aforementioned forgetful functor.

```agda
Codiscrete-topologies : Functor (Sets ℓ) (Topologies ℓ)
Codiscrete-topologies =
  cofree-objects→functor Codiscrete-topology-cofree

Topologies↪Sets⊣Codiscrete-topologies : Topologies↪Sets ⊣ Codiscrete-topologies {ℓ}
Topologies↪Sets⊣Codiscrete-topologies =
  cofree-objects→right-adjoint Codiscrete-topology-cofree
```
