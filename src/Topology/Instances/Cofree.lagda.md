---
description: |
  The cofree topology on a collection of subsets.
---
<!--
```agda
open import 1Lab.Reflection.Induction

open import Cat.Prelude

open import Data.Power

open import Topology.Base
```
-->

```agda
module Topology.Instances.Cofree where
```

# The cofree topology

:::{.definition #cofree-topology}
Let $O : \power{\power{X}}$ by a set of subsets of $X$.
The **cofree topology** generated by $O$ is the coarsest
topology on $X$ such that every element of $O$ is open.
:::


<!--
```agda
private variable
  ℓ : Level
  X Y : Type ℓ

open Topology-on
open is-continuous
```
-->

We can construct this topology in Agda by freely closing $O$ under
finite intersections and infinitary unions.

```agda
data Cofree-opens {X : Type ℓ} (O : ℙ (ℙ X)) : ℙ X → Type ℓ where
  basic     : ∀ (U : ℙ X) → U ∈ O → Cofree-opens O U
  top       : Cofree-opens O maximal
  intersect : ∀ {U V : ℙ X} → Cofree-opens O U → Cofree-opens O V → Cofree-opens O (U ∩ V)
  union     : ∀ (S : ℙ (ℙ X)) → (∀ (U : ℙ X) → U ∈ S → Cofree-opens O U) → Cofree-opens O (⋃ˢ S)
  trunc     : ∀ {U : ℙ X} → is-prop (Cofree-opens O U)
```

<!--
```agda
unquoteDecl Cofree-opens-elim-prop = make-elim-n 1 Cofree-opens-elim-prop (quote Cofree-opens)

instance
  HLevel-Cofree-opens
    : ∀ {O : ℙ (ℙ X)} {U : ℙ X}
    → H-Level (Cofree-opens O U) 1
  HLevel-Cofree-opens = prop-instance trunc
```
-->

```agda
Cofree-topology : ℙ (ℙ X) → Topology-on X
Cofree-topology O .Opens U = elΩ (Cofree-opens O U)
Cofree-topology O .maximal-open = pure top
Cofree-topology O .∩-open U-open V-open = ⦇ intersect U-open V-open ⦈
Cofree-topology O .⋃ˢ-open S S⊆Opens = pure (union S λ U U∈S → □-out! (S⊆Opens U U∈S))
```

Like all good constructions, the cofree topology generated by
$O : \power{\power{Y}}$ has a universal property: for every
$f : X \to Y$ and every topology $T$ on $X$, $\forall U,\, U \in O \Rightarrow f^{-1}(U) \in T$
if and only if $f$ is continuous with respect to the codiscrete topology.

```agda
cofree-⊆
  : ∀ {T : Topology-on X} {O : ℙ (ℙ Y)}
  → {f : X → Y}
  → is-continuous f T (Cofree-topology O)
  → O ⊆ Preimage (Preimage f) (T .Opens)

cofree-continuous
  : ∀ {T : Topology-on X} {O : ℙ (ℙ Y)}
  → {f : X → Y}
  → O ⊆ Preimage (Preimage f) (T .Opens)
  → is-continuous f T (Cofree-topology O)
```

The forward direction is almost by definition: if $f$ is continuous, then
it reflects every open in the codiscrete topology, and every element of $O$
is open.

```agda
cofree-⊆ {f = f} f-cont U U∈O =
  f-cont .reflect-open (pure (basic U U∈O))
```

The reverse direction requires a bit more thought, but only marginally so.
Suppose that some $U : \power{Y}$ is open in the cofree topology. We can
then invoke the elimination principle of `Cofree-open` to show that
$f^{-1}(U)$ is open in $T$: all of the closure conditions hold as $T$
is a topology, so it suffices to know that $U \in O \Rightarrow f^{-1}(U) \in T$.
```agda
cofree-continuous {T = T} {f = f} O⊆f[T] .reflect-open =
  elim! $
  Cofree-opens-elim-prop {P = λ {U} _ → Preimage f U ∈ T .Opens} (λ _ → hlevel 1)
    O⊆f[T]
    (T .maximal-open)
    (λ _ U∈T _ V∈T → T .∩-open U∈T V∈T)
    (λ S _ S⊆T → ⋃-open T (Preimage f ⊙ fst) λ V → S⊆T (V .fst) (V .snd))
```
